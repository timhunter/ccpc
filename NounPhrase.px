#!/usr/bin/perl
#
# Lightly postprocess the output of NounPhrase to make the basic results easier
# to import into other programs such as R
#
# Usage: ./NounPhrase.px NounPhrase.out.1 > NounPhrase.data

use strict;
use warnings;
no warnings 'uninitialized';

sub re_maybe_either {
    my ($re) = @_;
    return qr{(?:Nothing|Just\s*\((?:Left\s*\[[^\]\[\t]+\]|Right\s*$re)\))};
}

my $re_double                     = qr{([-+.e\d]+)};
my $re_maybe_either_double        = re_maybe_either($re_double);
my $re_maybe_either_double_double = re_maybe_either(qr{\($re_double,$re_double\)});

$, = "\t";
$\ = "\n";
print qw(Concept Relation Relatum
         r_np_npb_KL_ r_np_npb_KL
         r_np_npb_LK_ r_np_npb_LK
         l_npb_KL
         l_npb_LK);
while (<>) {
    print map { $_ // "NA" }
        m{^(\S+)\t (\S+)\t (\S+)\t
        $re_maybe_either_double_double\t
        $re_maybe_either_double_double\t
        $re_maybe_either_double\t
        $re_maybe_either_double$}xo;
}
